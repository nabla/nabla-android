plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'app.cash.paparazzi'
}

group buildConfig.groupId

android {
    namespace = 'com.nabla.sdk.messaging.ui'

    compileSdk buildConfig.compileSdk

    resourcePrefix "nabla"

    defaultConfig {
        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        aarMetadata {
            minCompileSdk buildConfig.compileSdk
        }
    }

    compileOptions {
        sourceCompatibility buildConfig.jvmTarget
        targetCompatibility buildConfig.jvmTarget
    }

    buildFeatures {
        viewBinding true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    lintOptions {
        checkDependencies true
        warningsAsErrors true
        abortOnError true
        disable 'RtlEnabled','VisibleForTests'
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    // we don't want it for test src, see https://stackoverflow.com/questions/64267076
    if (!it.name.contains("Test")) {
        kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"
    }

    kotlinOptions.freeCompilerArgs += "-opt-in=com.nabla.sdk.core.annotation.NablaInternal"
}

// Paparazzi requires Java 11 for unit test while https://github.com/cashapp/paparazzi/issues/409 is open
tasks.withType(Test).configureEach {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(11))
    })
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}

dependencies {
    api project(':messaging-core')
    api libs.androidx.constraintlayout
    api libs.androidx.lifecycle.viewmodel.savedstate
    api libs.recyclerview
    api libs.touchImageView
    api libs.google.material
    api libs.androidx.appcompat

    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.core.ktx
    implementation libs.coilCore
    implementation libs.coilVideo
    implementation libs.androidx.fragment.ktx
    implementation libs.exoplayer
    implementation libs.mimeTypeDetector

    testImplementation project(':core-test-fixtures')
    testImplementation project(':messaging-core-test-fixtures')
    testImplementation libs.roboelectric
    testImplementation libs.junit
    testImplementation libs.android.junit
    testImplementation libs.kotlin.test
}
