plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
}

group buildConfig.groupId

android {
    compileSdk buildConfig.compileSdk

    resourcePrefix "nabla"

    defaultConfig {
        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro", "${rootProject.projectDir.path}/serialization-rules.pro"

        aarMetadata {
            minCompileSdk buildConfig.compileSdk
        }

        vectorDrawables.useSupportLibrary true
    }

    compileOptions {
        sourceCompatibility buildConfig.jvmTarget
        targetCompatibility buildConfig.jvmTarget
    }

    buildFeatures {
        viewBinding true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    lint {
        warningsAsErrors true
        abortOnError true
        disable 'RtlEnabled','VisibleForTests'
        checkDependencies true
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed"
                exceptionFormat "full"
                showStackTraces true
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}

dependencies {
    lintChecks(project(":linter-rules"))
}

publishing.publications.all {
    if (it.name == 'apollo') {
        alias true
    }
}
